buildscript {
    repositories {
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath 'io.spring.gradle:propdeps-plugin:0.0.10.RELEASE'
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.20.0"
    id 'org.springframework.boot' version '2.1.0.RELEASE'
    id 'io.franzbecker.gradle-lombok' version '1.14'

//    id "org.liquibase.gradle" version "1.2.4"
//    id "org.sonarqube" version "2.5"
//    id "net.ltgt.errorprone" version "0.0.11"
}

//apply plugin: 'application'
//mainClassName = 'com.github.danielwii.starter.Application'

allprojects {
    group = 'com.github.danielwii'
}

// https://github.com/tbroyer/gradle-apt-plugin
allprojects { project ->
    project.plugins.withId("net.ltgt.apt") {
        try {
            // automatically apply net.ltgt.apt-idea whenever net.ltgt.apt is used
            project.apply plugin: "net.ltgt.apt-idea"
            // disable addAptDependencies (if you delegate build actions to Gradle)
            project.plugins.withType(JavaPlugin) {
                project.afterEvaluate {
                    project.idea.module.apt.addAptDependencies = false
                }
            }
        } catch (UnknownPluginException) {
            // ignore, in case an older version of net.ltgt.apt is being used
            // that doesn't come with net.ltgt.apt-idea.
        }
    }
}

subprojects {
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: "com.github.ben-manes.versions"   // 版本检测
    apply plugin: "io.franzbecker.gradle-lombok"

    apply plugin: 'propdeps'
    apply plugin: 'propdeps-maven'
    apply plugin: 'propdeps-idea'

    ext['jooq.version'] = '3.11.7'

    lombok {
        // 1.16.20 will cause
        // java.lang.ClassNotFoundException: com.sun.tools.javac.code.TypeTags
        version = "1.18.4"
    }

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    dependencies {
        // https://stackoverflow.com/a/43574427/3480445
        compile('javax.xml.bind:jaxb-api:2.3.1')
        compile('com.sun.xml.bind:jaxb-impl:2.3.0.1')
        compile('com.sun.xml.bind:jaxb-core:2.3.0.1')
        compile("javax.activation:activation:1.1.1")
    }

    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'
}
